// Defines the "target server", which is the server
// that we're going to hack. In this case, it's "joesguns"
var target = "joesguns";

// Defines how much money a server should have before we hack it
// In this case, it is set to 75% of the server's max money
var moneyThresh = getServerMaxMoney(target) * 0.75;

// Defines the maximum security level the target server can
// have. If the target's security level is higher than this,
// we'll weaken it before doing anything else
var securityThresh = getServerMinSecurityLevel(target) + 5;

// If we have the relaySMTP.exe program, use it to open the SMTP Port
// on the target server
if (fileExists("relaySMTP.exe", "home")) {
    relaysmtp(target);
}

// If we have the HTTPWorm.exe program, use it to open the HTTP Port
// on the target server
if (fileExists("HTTPWorm.exe", "home")) {
    httpworm(target);
}

// If we have the SQLInjection.exe program, use it to open the SQL Port
// on the target server
if (fileExists("SQLInject.exe", "home")) {
    sqlinject(target);    
}

// If we have the BruteSSH.exe program, use it to open the SSH Port
// on the target server
if (fileExists("BruteSSH.exe", "home")) {
    brutessh(target);
}

// If we have the FTPcrack.exe program, use it to open the FTP Port
// on the target server
if (fileExists("FTPCrack.exe", "home")) {
    ftpcrack(target);
}

// Get root access to target server
nuke(target);

// Infinite loop that continously hacks/grows/weakens the target server
while(true) {
    if (getServerSecurityLevel(target) > securityThresh) {
        // If the server's security level is above our threshold, weaken it
        weaken(target);
    } else if (getServerMoneyAvailable(target) < moneyThresh) {
        // If the server's money is less than our threshold, grow it
        grow(target);
    } else {
        // Otherwise, hack it
        hack(target);
    }
}
